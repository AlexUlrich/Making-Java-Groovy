apply plugin:'groovy'
apply plugin:'war'
apply plugin:'jetty'
apply plugin:'eclipse'
apply plugin:'eclipse-wtp'

repositories {
    mavenCentral()
}

httpPort = 8163
stopPort = 9451
stopKey = 'foo'

[jettyRun, jettyRunWar]*.daemon = true

task integrationTest(dependsOn: jettyRun) << {
    callServlets()
    jettyStop.execute()
}

private void callServlets() {
    String response = "http://localhost:$httpPort/HelloServlet/hello".toURL().text.trim()
    assert response == 'Hello, Servlet!'
    response = "http://localhost:$httpPort/HelloServlet/hellogs".toURL().text.trim()
    assert response == 'Hello from a Groovy Servlet!'
}

build.dependsOn integrationTest

task wrapper(type:Wrapper) {
    gradleVersion = '1.0-milestone-6'
}

def springVersion = '3.1.0.RELEASE'

dependencies {
    groovy "org.codehaus.groovy:groovy-all:1.8.5"
    providedCompile 'javax.servlet:servlet-api:2.5'
    
    testCompile "junit:junit:4.9"
    testCompile "org.springframework:spring-core:$springVersion"
    testCompile "org.springframework:spring-test:$springVersion"
    
}
